plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'io.spring.dependency-management' version '1.1.6'
}

// 프로젝트를 빌드할 때 그래들이 설정 프로세서를 사용하도록 설정
configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(17)
	}
}

// profile로 testdata를 추가해 demo.BookDataLoader가 실행될 수 있게 설정
// 인텔리제이에서 어플리케이션을 실행하면 안되고 ./gradlew bootRun 명령어를 사용해야 함
bootRun {
	systemProperty 'spring.profiles.active', 'testdata'
}

repositories {
	mavenCentral()
}

// 공통 속성이나 변수를 설정
ext {
	set('springCloudVersion', "2023.0.3") // 사용할 스프링 클라우드 버전 정의
	set('testcontainersVersion', "1.19.8") // 사용할 테스트컨테이너 버전을 지정
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'

	// 애플리케이션의 재시도 구현 라이브러리
	implementation 'org.springframework.retry:spring-retry'

	implementation 'org.springframework.boot:spring-boot-starter-actuator'
//	implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.projectlombok:lombok:1.18.22'
	// 플라이웨이 의존성 추가 시작
	implementation 'org.flywaydb:flyway-core'
	implementation 'org.flywaydb:flyway-database-postgresql' // 이 의존성 추가 안하면 Unsupported Database: PostgreSQL 예외 발생
	// 플라이웨이 의존성 추가 끝

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	// PostgreSQL 데이터베이스를 Docker 컨테이너로 손쉽게 실행할 수 있게 함
	testImplementation 'org.testcontainers:postgresql'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	testImplementation 'org.springframework.boot:spring-boot-starter-webflux'

	runtimeOnly 'org.postgresql:postgresql'
	annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
}

tasks.named('test') {
	useJUnitPlatform()
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
		mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}" // 테스트컨테이너 의존성 관리를 위한 BOM
	}
}